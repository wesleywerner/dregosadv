!% -SDX

! For those interested, the Maro River starts in the Arafura Sea, Indonesia,
! at coordinates 8°28'03.0"S 140°21'20.8"E

Release 1;
Serial "200410";

Constant Story "Dr Ego and the Egg of Man-Toomba";
Constant Headline
            "^An Interactive Fiction Story
             ^by Special Agent.^";

! Show the time instead of the turn number
Statusline time;

! Mention to dead players that they can UNDO the last action.
Constant DEATH_MENTION_UNDO;

! Maximum score possible
Constant MAX_SCORE = 10;

! Points for visiting a room or taking an item with the "scored" attribute.
Constant OBJECT_SCORE 1;
Constant ROOM_SCORE 1;

! The time which this story begins (60 * HH + mm)
Constant StoryStartTime 900; ! 15:00

! The number of turns to pass until the canoe ride ends
Constant TurnsInCanoe 12;

! When going to undefined direction while in a JungleRoom, the player gets lost.
! Reading the compass returns them back on track, but if the player lost
! the device or does not use it, these are the odds of finding their way back:
! 1 in OddsOfFindingWayWhenLost.
Constant OddsOfFindingWayWhenLost 16;

! Show the option to list AMUSING things you can try (at the End of the story)
Constant AMUSING_PROVIDED;

! Hide status line for debug builds.
! This streamlines the diffs used by unit tests.
#ifdef DEBUG;
[DrawStatusLine;];
#endif;

Include "Parser";
Include "VerbLib";

! TODO
! + Get NewbieGrammar.h included
! + Look in directions in most rooms
! + Listen and smell senses in most rooms
! + task_scores - DM$ $22
! + Changing weather in the sky object
! + Optimize entry descriptions for all rooms
! + Advent style "go to room" logic, DM4 $30 pg 220

! [ ================================================================ Atributes ]

! Applied to NPCs to indicate an active state of interest in the player.
Attribute curious;

! [ =========================================================== Custom Actions ]

[ WhipSub;

    if (bullwhip notin player)
        "Not without your bullwhip.";

    if (noun in player)
        "You could hurt yourself.";

    if (noun == player)
        "You are not into that kind of thing.";

    if (noun ofclass NonPlayerCharacterObject)
        <<Attack noun>>;

    if (parent(noun) ofclass NonPlayerCharacterObject)
        <<Attack noun>>;

    ! if (children(player) > 1)
    !     "You need your hands free to use the whip effectively.";
    
    ! Whipping succeeded, frighten the monkey if it is nearby
    monkey_frighten();

    ! Test if any custom after routines are handled.
    if (AfterRoutines()) return;

    ! Default response
    "You crack the whip at ", (the) noun, " -- *SNAP*";

];

Verb 'whip' * noun -> Whip;

! [ ================================================================= Routines ]

[ Initialise;

    ! Set the clock and turn progression rate
    SetTime(StoryStartTime, 1);

    ! Set the timer on the canoe
    StartTimer(canoe, TurnsInCanoe);

    ! Set the starting location
    location = canoe;

    ! Move objects to their starting places
    move canoe to river;
    move boatman to canoe;
    move monkey to fig_tree;
    
    ! Start Daemons
    StartDaemon(monkey);

    ! enable debugging actions:
    #ifdef DEBUG;
      ! allow skipping the long boat ride with GO FASTER
      move canoe_hasten to river;
    #endif;

    ! Give player inventory
    move lodestar to player;
    move bullwhip to player;
    move journal to player;

    ! Give the player a name and description
    player.short_name = "Dr Ego";
    player.description = "You are Dr Ego, Purveyor of Curiousities,
                            Explorer of Treasures, Adventurous Archeologist.";

    ! Print the preface text (for non debug builds)
    #ifndef DEBUG;
    print_ret "^April 10th, 1940.
            ^^Your journey started a few days ago after
            docking at Port Moresby, from there it was two day's ride on
            the back of a truck to Kerema, followed by a small aircraft
            flight and landing near the mouth of the Maro River.
            ^^You met your guide and his canoe, who will take you up-river
            to your final destination -- a remote and relatively uncharted
            area in New Guinea -- to find the legendary ", (name) golden_egg;
    #endif;
];

! Remember the last known location of the player, before they get lost
! in the jungle, and return the player to this good location when they
! find their way back.
Global last_known_location = 0;

! Print out amusing things to try (only available after game is won).
[ Amusing;
    print_ret "Here are some strange and fun things that you can try
            (if you haven't already):^
            ^1) Whip the water while in the canoe.
            ^2) Get lost in the jungle, then look at your compass.";
];

[ PressAnyKey k; @read_char 1 -> k; return k; ];

! [ ================================================================== Classes ]

Class IndoorRoom
with
    cant_go "You bump into the stone wall. Ouch!";

Class OutdoorRoom
    has light;

! The jungle room allows the player to "get lost" for a little while, but
! they always find their way back to the last known location.
! The lost_in_jungle object handles returning the player back safely.
Class JungleRoom
    class OutdoorRoom
    with
        ! Get lost when going in undefined direction
        n_to lost_in_jungle,
        nw_to lost_in_jungle,
        ne_to lost_in_jungle,
        s_to lost_in_jungle,
        sw_to lost_in_jungle,
        se_to lost_in_jungle,
        e_to lost_in_jungle,
        w_to lost_in_jungle,
        ! Record the last known good location
        after [;
            Go:
                if (location ~= lost_in_jungle)
                    last_known_location = location;
        ];

Class NonPlayerCharacterObject
with
    life [;
        Attack:
            print_ret "You are not fond of hurting other living things.";
    ];

Class NonPlayerHumanObject
    class NonPlayerCharacterObject
    with
        before [;
            Take:
                print_ret "You think human trafficing is deplorable.";
        ],
    has animate;

Class NonPlayerAnimalObject
    class NonPlayerCharacterObject
    with
        before [;
            Take:
                print_ret "You think animal trafficing is deplorable.";
        ],
    has animate;

! A class that prevents the player from taking what isn't theirs.
Class NotYourPropertyObject
    with
        before [;
            Take:
                print_ret (The) noun, " is not yours to take.";
        ];

! Tools the player would consider essential, cannot be dropped by default.
Class EssentialObject
    with
        article "your",
        before [;
            Drop, ThrowAt:
                print_ret "You would not dream of leaving ",
                            (a) noun, " behind.";
            Taste, Eat:
                print_ret (A) noun, " is not food.";
        ];

! Provides the scenery attribute
Class ScenicRockObject
with
    before [;
        Take:
            if (noun has pluralname)
                print_ret (The) self, " are way too large and heavy to move.";
            else
                print_ret (The) self, " is way too large and heavy to move.";
    ],
has scenery static;

! Provides the scenery attribute
Class ScenicTreeObject
with
    before [;
        Take:
            print_ret (The) self, " is too firmly rooted in the earth.";
    ],
has scenery static;

! Provides the scenery attribute
Class ScenicPlantObject
with
    before [;
        Take:
            print_ret "You have no need for ", (the) self, ".";
    ],
has scenery static;

! Something edible
Class EdibleFruitObject has edible;

! [ ================================================================== Objects ]

Object canoe "canoe"
    with
        name 'boat' 'canoe' 'oar',

        ! Room Description when outside the canoe
        initial [;
            print_ret (A) boatman, " sits in the canoe gazing out over the
            moving river, the salty water gently lapping the sides of his
            craft.";
        ],

        description "It is a small wooden canoe, it looks hand built.
                    A fishing spear lies inside the boat.",

        before [;

            ! Prevent the player from exiting the craft before it has shored.
            Go, Exit:
                if (parent(canoe) == river) {
                    print "~We will reach your destination soon enough~ ",
                            (the) boatman, " says with a wide grin.^";
                    return 2;
                }

            ! Prevent the player from re-entering the craft.
            Enter:
                if (player in canoe)
                    print_ret "Did you forget, you are already in the canoe.";
                else
                    print_ret "You just got out, and you are eager to start
                                looking for ", (the) golden_egg, ".";
        ],

        ! Print flavor text during the trip in the canoe
        each_turn [;
            if (self.time_left == 2 && location == river)
                print_ret "^~We are almost there, ", (a) player, "~, ",
                            (a) boatman, " says.";
        ],

        ! Initialise TurnsInCanoe timer to 0
        time_left,

        ! called when TurnsInCanoe timer expires
        time_out [;
            if (location == river) {
                ! The "1" parameter moves player silently
                ! so that we can reposition the canoe then call look.
                ! DM4 pg 21
                PlayerTo(river_shore, 1);
                move canoe to river_shore;
                move player to canoe;
                print "^~Here we are~, ", (a) boatman,
                        " says -- the canoe comes to a stop on the shore.^";
                <<Look>>;
            }
        ],
  has   enterable static container open;

NotYourPropertyObject boatmans_spear "fishing spear" canoe
    with
        name 'fishing' 'rod' 'spear' 'kalawai',
        description "A traditional fishing spear made from wood, it has three
                    metal prongs at the business end.",
    has scenery;

! A debug-only object that provides the GO FASTER command to skip the ride.
Object canoe_hasten
    with
        name 'faster',
        before [;
            Enter:
                canoe.time_left = 0;
                print_ret "You hasten your journey.";
        ],
    has scenery enterable;

Object golden_egg "Golden Egg of Man-Toomba"
    with
        name 'gold' 'golden' 'egg',
        description "It shimmers with golden light.";

! The word "compass" is a reserved keyword in Inform, as such we use "lodestar"
! to identify the object as it lives in the player's inventory.
! The literal, albeit archaic, meaning of "lodestar" is "a star that leads or
! guides; especially : the North Star." (The first half of the word derives from
! the Middle English word "lode," meaning "course.") Both the literal and the
! figurative sense ("an inspiration or guide") date back to the 14th century, the
! time of Geoffrey Chaucer. The literal sense fell out of use in the 17th century,
! and so, for a while, did the figurative sense - but it appeared again 170 years
! later, when Sir Walter Scott used it in his 1813 poem The Bridal of Triermain.
EssentialObject lodestar "compass"
    with
        name 'compass',

        ! display a random description
        rnd_no 0,

        ! one-time bonus point when using the lodestar to navigate while lost.
        used_scored false,
        
        ! Track if the compass has been dropped before
        has_dropped false,

        description [;
        
            ! Test for posession
            if (self notin player) print_ret "You do not have ", (a) self, ".";

            ! Return the player to the last known good location
            if (location == lost_in_jungle) {
                if (~~self.used_scored) {
                    self.used_scored = true;
                    score = score + 1;
                }
                print "^", (A) self, " guides you back to safety.^";
                ! parameter "2" honors brief room descriptions - DM4 pg 21
                PlayerTo(last_known_location, 2);
                return true;
            }

            ! Display a random text when examining this.
            self.rnd_no = random(5); ! 1..n
            switch (self.rnd_no) {
                1: print "You are exactly where you need to be.^";
                2: print "The needle points towards north.^";
                default:
                    print (A) self, " helps you navigate while in the jungle.^";
            }

            ! The boatman notices your action.
            if (player in canoe && boatman.noticed_compass == false) {
                boatman.noticed_compass = true;
                print "^", (A) boatman, " notices ", (a) self, " -- ~My father
                            had one just like that~ he says.^";
            }
            
            ! The monkey notices your action
            if (~~ObjectIsUntouchable(monkey, true)) {
                print (The) monkey, " seems very interested in the device.^";
            }
            
        ],
        
        before [;
            Take, Remove:
                if (self in monkey) {
                    if (parent(monkey) == location) {
                        print "When you approach the small critter it deftly 
                        moves out of your reach. ";
                        ! monkey_forget_about_food();
                        monkey_frighten();
                        monkey_speak(random(10));
                        print_ret "";
                    } else {
                        print_ret "It is out of your reach.";
                    }
                }
        ];

! Also see the custom 'WhipSub' action.
EssentialObject bullwhip "bullwhip"
    with
        name 'bullwhip' 'whip',
        description "A leather whip made of braided strips, you can use it to
                    WHIP SOMETHING to grab items out of reach, or to grip a
                    fixture to swing and pull yourself over short distances.";

EssentialObject journal "journal"
    with
        name 'diary' 'journal' 'book',
        description "It is your journal filled with your research notes.
                    You can CONSULT IT ABOUT any TOPIC.";

EssentialObject torch "torch"
with
    name 'torch',
    description "It is a crude torch that you constructed from a stick and
        some old cloth, dipped in an oily substance."
    has ~light;

! The sky is visible in all outdoor areas.
Object sky "sky"
    with
        name 'sky' 'clouds' 'weather' 'sun',
        description "Rolling clouds move overhead.",
        found_in [;
            return (location ofclass OutdoorRoom);
        ],
    has scenery;

! The jungle is visible in all JungleRooms
Object jungle_scenery "jungle"
    with
        name 'jungle' 'foliage' 'plant' 'plants' 'bush' 'brush' 'shrub',
        description "The jungle foliage is dense and lush.",
        found_in [;
            return (location ofclass JungleRoom);
        ],
        before [;
            Attack:
                print_ret "You shout and wave your fist at ", (the) self;
        ],
    has scenery;

Object fish "fish"
    with
        name 'fish',
        initial "A fish flops around inside the canoe.",
        description "It looks like a black bass.",

        ! Tracks if the fish was caught
        caught false,

        ! try catch the fish with the whip
        try_catch [;
            if (self notin canoe && self.caught == false) {
                move self to canoe;
                self.caught = true;
                print_ret "*SNAP* -- As you crack the whip into the water
                        you snag a fish!
                        ~Mogillo!~ ", (a) boatman, " shouts excitedly.
                        ~What are the chances!~";
            }
        ],

        before [;
            Take: print_ret "You decide to leave the fish for ",
                    (a) boatman, ".";
        ],

        after [;
            Insert:
                if (second == river_water) {
                    print_ret "You put the fish back into ", (the) river_water,
                                ". ~Ai, there goes supper~ ",
                                (a) boatman, " says.";
                }
        ],

        each_turn [;
            if (self in canoe && random(3) == 1) {
                print "^A fish flops around inside the canoe.^";
            }
        ];

! [ ==================================================== Non Player Characters ]

! [ ------------------------------------------------------------------- Monkey ]
NonPlayerAnimalObject monkey "monkey"
with
    name 'monkey',
    
    ! Hiding away counter
    fright_turns 0,
    
    ! First fright state
    first_fright true,
    
    ! When in the presence of a banana, take a few turns before taking it.
    reach_for_food_counter 0,
    
    initial [;
        print "There is a monkey here. ";
        if (lodestar in self)
            print "It has ", (a) lodestar, ".";
        print_ret "";
    ],
    
    description [;
        if (lodestar in self)
            print_ret "The small creature is fascinated by ", (a) lodestar, ".";
        else
            print_ret "It is a small grey monkey with big brown eyes. 
                It stares back at you.";
    ],
    
    life [;
        Ask, Answer, Tell:
            monkey_speak(random(10));
            print_ret "";
        Give:
            if (noun ofclass EssentialObject)
                print_ret "There is no way are you giving ", (a) noun, 
                " to ", (the) self, "!";
            else
                print_ret "You offer ", (the) noun, " to the small critter, 
                it pretends to ignore your good gesture.";
        Show:
            if (noun ofclass EdibleFruitObject)
                print_ret "The small monkey head swivels side-to-side as you
                wave ", (the) noun, ", showing it off. ", (The) self, "
                likes what it sees but looks hesitant to go near you.";
            if (noun ofclass EssentialObject)
                print_ret "It seems curious about ", (the) noun, ".";
        ThrowAt:
            if (noun ofclass EdibleFruitObject)
                print_ret "Maybe it would be better to just drop ", (the) noun,
                ".";
            else
                <<Attack self>>;
    ],
    
    orders [;
        Give:
            if (noun == lodestar) {
                monkey_speak(random(10));
                print_ret " it replies, gripping the device tightly.";
            }
    ],
    
    ! Monkey Movements
    ! TODO routinize monkey daemon
    daemon [look_item fruit;
    
        ! Move to the clearing if the player is in the tree
        if (self in fig_tree && location == fig_tree_top) {
            print "^The monkey is not impressed, it climbs down the tree.^";
            give self curious;
            move self to clearing;
            return;
        }
        
        ! No action if not curious
        if (monkey hasnt curious) return;
        
        ! Flee when frightened
        if (self.fright_turns > 0 && monkey_near_player(true)) {
            move self to fig_tree;
            ! Print flavor text when monkey is frightened the first time
            ! from a whip action.
            if (self.first_fright && action == ##Whip) {
                self.first_fright = false;
                print "^The piercing sound startles ", (the) self, 
                ", it launches up into the air and ";
            } else {
                print "^", (The) self;
            }
            if (location == clearing) {
                print_ret " scrambles up ", (the) fig_tree, ".";
            } else {
                print_ret " scrambles out of sight.";
            }
        }
        
        ! Rebuild confidence
        if (self.fright_turns > 0) {
            self.fright_turns = self.fright_turns - 1;
            return;
        }

        ! Climb down the tree
        if (self in fig_tree) {
            move self to clearing;
            if (location == clearing) {
                print_ret "^", (The) self, " climbs down from ", (the) fig_tree, ".";
            }
        }
    
        ! No action if the player is not nearby.
        if (~~monkey_near_player(false)) return;
        
        ! Scan for any edibles in the room
        fruit = nothing;
        objectloop(look_item in parent(self)) {
            if (look_item ofclass EdibleFruitObject)
                fruit = look_item;
        }
        
        ! If there is a fruit in reach
        if (fruit && parent(self) == parent(fruit)) {
            self.reach_for_food_counter = self.reach_for_food_counter + 1;
            print "^";
            switch (self.reach_for_food_counter) {
                1:  
                    print_ret (The) self, " stares at ", (the) fruit,
                    ", then glares at you, looks at the banana again,
                    smacking it's lips.";
                2:  
                    print_ret (The) self, " edges closer to ", (the) fruit,
                    " while keeping one eye on you.";
                3:  
                    print (The) self, " grabs ", (the) fruit, 
                    ", stuffing its face with the delicious fruit.";
                    if (lodestar in self) {
                        ! remove the fruit and return the player's posession
                        move fruit to garden_trees;
                        move lodestar to location;
                        ! climb back into the tree and reset counters
                        monkey_forget_about_food();
                        ! move self to fig_tree;
                        ! give self ~curious;
                        print_ret " It seems distracted and left ", 
                        (the) lodestar, " lying on the ground.";
                    }
                    print_ret "";
            }
        }
        
        ! Let the monkey do something when in the presence of the player
        if (parent(self) == parent(player)) {
            monkey_action();
        } else {
            ! When not with the player, follow the player.
            if (random(4) == 1) {
                ! Move to the player's location
                print (The) self, " appears nearby.^";
                move self to parent(player);
            }
        }    
    ],
    has transparent;

[monkey_forget_about_food;
    monkey.reach_for_food_counter = 0;
];

[ monkey_frighten;
    if (monkey_near_player(true)) {
        monkey.fright_turns = 4;
        ! Reset turn to reach for the banana
        monkey_forget_about_food();
    }
];

[ monkey_near_player same_room;
    if (same_room) {
        return parent(monkey) == parent(player);
    }
    else {
        ! Test the Player's location
        return location == clearing || location == old_hut 
            || location == garden || location == jungle_path;
    }
];

[ monkey_action n;

    ! only some times
    n = random(100);
    if (n > 30) return;
    
    ! begin a new line
    print "^";
    
    ! The monkey has the compass and half the time
    if (lodestar in self && n > 15) {
        switch (n % 5) {
            0: print_ret (The) self, " stares at the moving needle on ", 
                (a) lodestar, ".";
            1: print_ret (The) self, " fidgets with ", (a) lodestar, ".";
            2: print_ret (The) self, " taps ", (a) lodestar, " on the ground.";
            3: print_ret (The) self, " bites on ", (a) lodestar, ".";
            4: print_ret (The) self, " sniffs ", (a) lodestar, ".";
        }
    } 
    else { ! The monkey does not have the compass, or the other half the time
        switch (n % 4) {
            0: print_ret (The) self, " scratches itself.";
            1: print_ret (The) self, " picks something off the ground, 
                and eats it.";
            2: print_ret (The) self, " makes grunting noises.";
            3: print_ret "~Ook ook~ says ", (the) self, ".";
        }
    }
];

! hoot, scream, howl, coo, rumble, bark, pant, grunt, whoop, screech, gibber.
[ monkey_speak n;
    switch (n % 8) {
        1: print "~*Coo*coo*~";
        2: print "~*Whoop*whoop*~";
        3: print "~*Howls*~";
        4: print "~*Pants*Grunt*~";
        4: print "~*Gibbers*~";
        6: print "~*Screech*~";
        7: print "~*Rumble*~";
        default: print "~*Grunts*~";
    }
];

! [ ------------------------------------------------------------------- Boatman ]
NonPlayerHumanObject boatman
    with
        name 'guide' 'rom' 'man',
        article "your",
        short_name "guide",

        ! NPC noticed you examining your compass
        noticed_compass false,

        ! Hide the room description (in the boat you see your guide)
        ! TODO move desccriptions from rooms into here.
        initial [;
            return true;
        ],

        each_turn [;
            ! Only while on the river
            if (parent(canoe) == river && self in canoe)
                boatman_each_turn_action(random(10));
        ],

        description [;
            print "A native to Papua New Guinea, he seems to navigate
                    this river like he has done it for all his years.^";
            if (self hasnt proper) {
                print "^Perhaps you should SAY HI.^";
            }
        ],

        life [;
            Answer:
                return boatman_answers(noun);
            Ask:
                return boatman_ask(second);
        ],
;

[ boatman_ask second;
    switch (second) {
        'canoe', 'boat':
            print_ret "~I have the canoe for many years now, built it myself~, ",
                        (a) self, " says proudly.";
        'river', 'maro', 'water':
            print_ret "~The ancient waters of the Maro will swallow you whole if
                        you try to swim~, he says mournfully.";
        'father':
            print_ret "~He taught me how to build boats and how to fish...
                        taught me everything to live and survive out here~";
        'fishing', 'spear':
            print_ret "~We call that a Kalawai, good for catching dinner!~";
    }
    print_ret (The) self, " grins at you and nods.";
];

[ boatman_answers noun;
    switch (noun) {
        'hello', 'hi':
            if (self has proper)
                print_ret (A) self, " looks baffled at your repeated greeting,
                            but he obliges you with a big grin anyway.";
            else {
                print "You introduce yourself to ", (a) self, ". ^^~Nice to meet
                        you, ", (a) player, "~, he says with a grin, ~I am
                        Rompinjimp-Nanganbia Tiki~.^";
                boatman_rename();
                print "^~You can call me ", (a) self, "~, he adds.^";
                return true;
            }
        'bye', 'goodbye', 'cheers':
            if (location == river)
                print_ret "No need to say goodbye to ", (a) self, " before
                            you reach your destination.";
            if (location == river_shore)
                print_ret "~Safe travels ", (a) player, ", I hope you find what
                            you are looking for!~, ", (a) self, " waves.";
    }
];

[ boatman_rename;
    boatman.short_name = "Rom";
    give boatman proper;
];

[ boatman_each_turn_action n;
    switch (n) {
        1, 3:
            print "^", (A) boatman, " paddles with the oar.^";
        5:
            if (fish in canoe)
                print "~We will have fish for dinner tonight~, ",
                    (a) boatman, " says hungrily.^";
        7:
            print "^", (A) boatman, " looks across the salty water,
                into the distance.^";
        9:
            if (random(2) == 1)
                if (boatman.noticed_compass == true)
                    print "^~I still have my father's compass...~, ",
                        (a) boatman, " reflects nostalgically.^";
    }
];

! [ ==================================================================== Rooms ]

! [ -------------------------------------------------------------- Lost Jungle ]
JungleRoom lost_in_jungle "Lost in the Jungle"
with
    description "You don't know where you are.",
    before [;
        Go:
            if (random(OddsOfFindingWayWhenLost) == 1) {
                print "^You happen to find your way back.^";
                ! parameter "2" honors brief room descriptions - DM4 pg 21
                PlayerTo(last_known_location, 2);
                return true;
            }
            else
                print_ret "You fumble through the jungle.";
    ];

! [ --------------------------------------------------------------- Maro River ]
OutdoorRoom river "The Maro River"
with
    description [;
        print_ret "You are in ", (a) canoe, " on the Maro river. ",
                    (A) boatman, " is with you, rowing the craft.";
    ];

! Scenic jungle when on the river
Object distant_jungle_scenery "jungle" river
with
    name 'jungle' 'foliage' 'plant' 'plants' 'bush' 'brush' 'shrub',
    description "The jungle foliage is dense and lush.",
    has scenery;

! [ -------------------------------------------------------------- River Shore ]
OutdoorRoom river_shore "River Shore"
with
    description [;
                if (player in canoe)
                    print_ret "The canoe has stopped ashore, you can get out now.";
                else
                    print_ret "You are on the river shore. A roughly cut path
                                leads North into the jungle.";
                ],
    n_to jungle_path;

Object river_water "Maro River"
with
    name 'maro' 'river' 'water',
    article "the",
    ! TODO move this detailed description to an entry in the journal.
    description "The Maro river flows from north-east to south-west, into
                the Arafura Sea, it runs for over 200 km and is between 50 and
                900 m wide. The river is strongly tidal for most of its length
                and its lower reaches are affected by salt water. It is a
                complex system of swamps and oxbow lakes which hosts a large
                number of birds and reptiles.",
    found_in  river river_shore,
    after [;
        Whip:
            return fish.try_catch();
    ],
    has container open scenery;

! [ -------------------------------------------------------------- Jungle Path ]
JungleRoom jungle_path "Jungle Path"
with
    description "Trees of all sizes and leaves of all shapes fill every
                available space of the jungle, this roughly cut path will
                be indistinguishable from the rest in a few months.
                ^^The river shore is back South, you notice a clearing
                in the jungle to the West.",
    w_to clearing,
    s_to river_shore;

! [ ----------------------------------------------------------------- Clearing ]
JungleRoom clearing "In a Clearing"
with
    description [;
        if (self hasnt visited)
            print "You are in a natural clearing, the ground a part of a large
                bedrock providing some relief from the dense jungle. ";
        print "A large fig tree stands here proudly. ";
        if (monkey in fig_tree)
            print "A monkey sits in ", (the) fig_tree, ".";
        print "^^A jungle path is due East.
            A small structure is visible to the South. ";
        if (self.ne_to ofclass JungleRoom)
            print "Stone stairs are due Northeast.";
        print_ret "";
    ],
    e_to jungle_path,
    s_to old_hut,
    u_to fig_tree_top,

    before [;
    
        ! climb up => go up
        Climb:
            if (noun == u_obj)
                <<Go noun>>;
        
        ! Describe climbing up the tree
        Go:
            if (noun == u_obj) {
                print "You climb up the strong branches of the fig tree.^";
            }
    ];

! [ -------------------------------------------------------------- Top Of Tree ]

OutdoorRoom fig_tree_top "Top of Tree"
with
    description [;
        if (self hasnt visited)
            print "The view from up here is breathtaking! ";
        print_ret "The vantage point allows you to see in all directions.";
    ],
    cant_go "The only way is down.",
    d_to clearing,
    
    before [;
    
        Go:
            if (noun == d_obj && lodestar in player && ~~lodestar.has_dropped) {
                print "^As you descend you drop ", (a) lodestar, 
                    " to the ground below. CLINK!
                    ^^ --- more ---^";
                lodestar.has_dropped = true;
                move lodestar to monkey;
                PressAnyKey();
                return false;
            }
    
        Climb:
            ! climb down => go down
            if (noun == d_obj)
                <<Go d_obj>>;
        
        ! Look in directions
        Examine:
            if (noun == ne_obj) {
                clearing.ne_to = valley;
                score = score + 1;
                print_ret "There! Stone stairs up the hill, in the distance to ",
                    (a) noun, ".";
            }
            ! Any other direction gives a canned response.
            ! Note that we *must* test for CompassDirection here
            ! else the Examine intercept will catch examining nouns too.
            if (noun ofclass CompassDirection)
                print_ret "Only more jungle to ", (a) noun ;
    ];

ScenicTreeObject fig_tree "fig tree"
with
    name 'fig' 'tree' 'ficus',
    description [;
        print "The fig tree, also known as Ficus Obliqua, is taller than 
            most of the surrounding jungle.";
        if (location == clearing)
            print " The view from up there must be great.";
        print "^";
    ],
    found_in clearing fig_tree_top,
        
    ! climb fig_tree => go up
    before [;
        Climb:
            if (location == clearing)
                <<Go u_obj>>;
            if (location == fig_tree_top)
                <<Go d_obj>>;
    ],
    has container open light;
    
! [ ------------------------------------------------------------------ Old Hut ]
OutdoorRoom old_hut "An Old Hut"
with
    description "You are inside what appears to be a delapidated hut. The
                roof long since collapsed, it provides little protection
                from the elements.
                You see a clearing to the North
                and an exit to the Southwest.",
    cant_go "The only exits are North and Southwest.",
    n_to clearing,
    sw_to garden;

! [ ------------------------------------------------------------------- Garden ]
JungleRoom garden "Garden"
with
    description "You stand in an unkempt garden, whoever kept this garden hasn't
    been here for a very long time. You see some vegetable plants and fruit
    trees. A hut is to the Northeast.",
    ne_to old_hut;

ScenicPlantObject "vegetable plants" garden
with
    name 'veg' 'vegetable' 'vegetables' 'plant' 'plants',
    description [;
        print "The vegetable plants in the garden have grown wild without
        anybody around to harvest them. ";
        if (pumpkin has concealed) {
            print "You notice a pumpkin on the ground, hiding under some large
            green leaves.";
        }
        print_ret "";
    ];

ScenicPlantObject garden_trees "fruit trees" garden
with
    name 'fruit' 'cherry' 'tree' 'trees',
    
    description "You see a cherry tree but it is not bearing any fruit, the
    banana tree next to it however has a bunch of fruit.",
    
    before [;
        Whip:
            if (parent(banana) ~= garden_trees)
                print_ret "You don't need another banana right now.";
    ],
    after [;
        Whip:
            move banana to garden;
            banana.whip_count = banana.whip_count + 1;
            switch (banana.whip_count) {
                1: print_ret "You take careful aim at the bunch of bananas
                up in the tree, swing your arm in a smooth arch and *SNAP*
                -- a single banana drops to the ground.";
                2: print_ret "*SNAP* -- Another banana drops to the ground.
                Years of using your whip is finally paying off.";
                default: print_ret "*SNAP* -- You deftly whip a banana off
                the tree.";
            }
    ],
    has supporter;

EdibleFruitObject banana "banana" garden_trees
with
    name 'banana' 'bananas',
    whip_count 0,
    eaten false,
    description [;
        if (parent(self) == garden_trees)
            print_ret "A bunch of wild bananas hang from the top of the tree.";
        else
            print_ret "It is a wild banana with a bright green and yellow skin.";
    ],
    before [;
        Take:
            if (parent(self) == garden_trees)
                print_ret "The bananas are way up in the tree, out of reach.";
        Eat:
            if (self.eaten)
                print_ret "You have had your fill of bananas.";
        Whip:
            <<Whip garden_trees>>;
    ],
    after [;
        Eat:
            move banana to garden_trees;
            banana.eaten = true;
            print_ret "You peel the banana and take a bite, the fruit is
            firm and sweet with a subtle flavor. You finish eating the rest
            of the delicious fruit - You feel revitalised.";
    ];

Object pumpkin "pumpkin" garden
with
    name 'pumpkin' 'pumpkins',
    description "It is a small round and green pumpkin.",
    before [;
        Eat:
            print_ret "It looks delicious and fresh, but would be better served
            cooked.";
    ],
    after [;
        Take:
        if (self hasnt moved) {
            give self ~concealed;
            print_ret "You carefully break the stem and take the small round
            pumpkin.";
        }
    ],
    has concealed edible;

! [ ------------------------------------------------------------- Stone Stairs ]
JungleRoom valley "Valley"
with
    description "You in a lush jungle valley, stairs carved out of stone
                lead up to the top. A path leads back South.",
    u_to cliff,
    s_to clearing,
    cant_go "The only ways out are South or Up (the stairs).";

! [ -------------------------------------------------------------------- Cliff ]
JungleRoom cliff "Cliff"
with
    description "You are on top of a cliff, a rope bridge crosses a deep gorge
    to the East. Stone steps lead down the hill.",
    d_to valley,
    e_to waterfall;

ScenicRockObject stone_steps "stone steps"
with
    name 'stone' 'stairs' 'steps' 'hill',
    description "The individual steps are carved from stone and laid out to form
    a staircase on the hill.",
    found_in valley cliff,
    door_to [;
        if (self in valley) return cliff;
        return valley;
    ],
    door_dir [;
        if (self in valley) return u_to;
        return d_to;
    ],
    before [;
        ! Allow climbing the steps to go their way
        Climb:
            <<Go (self.door_dir())>>;
    ],
    has pluralname door open;

Object "bridge" cliff
with
    name 'rope' 'bridge',
    description "The bridge is made from plait ropes and extends to the East.",
    before [;
        ! Enter also covers the 'cross' verb
        Enter:
            <<Go e_obj>>;
    ]
    has scenery;

! [ --------------------------------------------------------------- Waterfall  ]
JungleRoom waterfall "Waterfall"
with
    description "You stand beside a beautiful waterfall. The water rushes past
        you from somewhere up above, it cascades over a rocky ledge to the South.
        You see a rope bridge to the West.",
    n_to,
    
    before [;
        Go:
            if (noun == s_obj)
                <<JumpOver waterfall_ledge>>;
    ];

ScenicRockObject waterfall_ledge "ledge" waterfall
with
    name 'water' 'fall' 'waterfall' 'rocky' 'ledge' 'edge',
    description "You peer over the waterfall and see the water cascading
        down into a pool below. It looks safe to jump over it.",
    before [;
        JumpOver:
            print "You take a breath and jump over the edge of the
                waterfall...^";
            last_known_location = plunge_pool;
            ! parameter "2" honors brief room descriptions - DM4 pg 21
            PlayerTo(plunge_pool, 2);
            return true;
    ];

! [ -------------------------------------------------------------- Plunge Pool ]
JungleRoom plunge_pool "Plunge Pool"
with
    description [;
        if (self hasnt visited)
            print "^** SPLASH **^
                ^You land safely in the pool below.
                Wading to the shore, you wring out your shirt.
                That was very refreshing!^^";
        print_ret "There is a clear pool here, a beautiful waterfall cascades
            down from above into it.";
    ],

    ! GO IN enters the alcove, if discovered.
    ! in_to [;
    !     if (alcove hasnt scenery)
    !         <<Enter alcove>>;
    !     else
    !         "You don't feel like another swim.";
    ! ],
;

ScenicRockObject "temple" plunge_pool
with
    name 'temple' 'stone' 'building',
    initial "There is an ancient temple here.",
    description "The temple is built of carved stone blocks, there is an opening
        that serve as the entry way.",
    before [;
        Enter:
            ! parameter "2" honors brief room descriptions - DM4 pg 21
            return PlayerTo(temple, 2);
    ],
    has ~scenery;

ScenicRockObject alcove "alcove" plunge_pool
with
    ! discovered false, ! this is hidden at first
    name 'secret' 'alcove',
    initial "You notice an alcove behind the cascading water.",

    description [;
        print "This secret little alcove is not so secret anymore. ";
        if (torch in player && torch has light)
            print "You see the passage beyond the alcove leading underground.^";
        else if (torch in player && torch hasnt light)
            print "It is pitch black in there. If only your torch was lit.^";
        else if (torch notin player)
            print "It appears to lead underground, but it is pitch black in
                there, you can't make out where it leads.^";
    ],

    before [;
        Enter:
            if (torch in player && torch has light)
                print "You steady your torch and head down the dark passage.^";
            else if (torch in player && torch hasnt light)
                print_ret "It is pitch black in there. If only your torch was lit.";
            else if (torch notin player)
                print_ret "You would rather not enter without a light.";
    ],

    after [;
        Enter:
            ! parameter "2" honors brief room descriptions - DM4 pg 21
            return PlayerTo(tomb, 2);
        Whip:
            print_ret "*SNAP* echos *snap* your *snap* whip.";
    ],

    has enterable static;

! [ ------------------------------------------------------------------- Temple ]
IndoorRoom temple "Temple"
with
    description "You are inside an old stone temple, large openings in the
        ceiling provide ample natural light for you to see.
        You hear water falling from the exit to the North.",
    n_to plunge_pool,

    before [;
        Exit:
            ! Exit the temple
            <<Go n_obj>>;
        Go:
            if (noun == out_obj)
                <<Go n_obj>>;
                
        ! Go:
        !     if (alcove has scenery) {
        !         give alcove ~scenery;
        !         print_ret "You discover the secret alcove! (WIP).";
        !     }
    ],

    has light;

! [ --------------------------------------------------------------------- Tomb ]
IndoorRoom tomb "Underground Tomb"
with
    description "You are inside an ancient tomb, the light of your torch
        make engraved runes in the walls seem to dance all around you.
        A narrow stone staircase leads up.",
    has light;

! [ ========================================================= Room Connections ]
! [ For auxiliary objects the player can enter to travel between rooms ]

! TODO add a hut_entry_obj found_in clearing and garden.
!       use the initial property to display "a structure is to the south/north"
!       depending which location is described.
!       include this in movement.spec


! [ ================================================================ Debugging ]
! These portals provide a way to quickly jump to locations in our spec scripts.
#ifdef DEBUG;
ScenicRockObject clearing_portal clearing with name 'clearing' 'portal';
ScenicRockObject garden_portal garden with name 'garden' 'portal';
ScenicRockObject valley_portal valley with name 'valley' 'portal';
#endif;

! [ ================================================================== Grammar ]
Include "Grammar";
! Include "NewbieGrammar";

! [ Extend Grammar ----------------------------------------------------------  ]

! Print help when trying to "go up the noun".
[ GoUpDownNounSub;
    print "To go up or down ", (the) noun, " type ~go up~ or ~go down~. 
        To climb ", (the) noun, " type ~climb ", (name) noun, "~^";
];

Extend 	'go'		* 'up'/'down' noun				 -> GoUpDownNoun;
Extend 	'climb'		* 'up'/'down' noun				 -> GoUpDownNoun;

! Print help when trying to "talk to noun"
[ TalkToNounHelpSub;
    print "To ask ", (the) noun, " something type 
        ~ask ", (the) noun, " about (thing or topic)~. 
        To say something to ", (the) noun, " type
        ~say (noun or topic)~.^";
];

Verb    'talk'      * 'to' noun                     -> TalkToNounHelp;